package "stabilitytest"

import "nova/time/Timer"
import "nova/thread/Thread"
import "nova/io/File"
import "nova/io/FancyOutputStream"
import "nova/star/Button"
import "nova/star/Label"
import "nova/star/Panel"
import "nova/star/Color"
import "nova/star/ScrollBar"

import "novex/nest/ui/ResultWindow"
import "novex/nest/ui/ResultBar"
import "novex/nest/TestResult"

[TestSuite [
    ExceptionStability,
    SyntaxStability,
    ClosureStability,
    PolymorphismStability,
    LambdaStability,
    ToStringStability,
    AssignmentStability,
    StaticImportStability,
    InnerClassStability,
    ExternalInnerClassStability,
    PrimitiveOverloadStability,
    FancyOutputStreamTests,
    FirstClassFunctionStability,
    LibraryLoadingStability,
    IntervalStability,
    BoundedIntervalStability,
    BoundlessIntervalStability,
    CastStability
]]
[Target c]
[TestSuite [
    TimeStability,
    ThreadStability,
    FileStability,
    NetworkStability,
    RegexStability
]]
class StabilityTest {
    public static main(String args[]) {
        let test = new StabilityTest()

        Timer timer = new Timer().start()
        
        let results = new TestResult[]

        let suites = args.skip(2)

        if (suites.count > 0) {
            suites.forEach({
                native TestResult[] suiteResults
                var count = 0

                external js {
                    #{suiteResults} = [];

                    var instance = #{System.jsCreateInstance(_)}

                    instance.runTests((r) => {
                        #{suiteResults}.push(r);
                        #{count}++;
                    });
                }

                for (i in 0..count) {
                    results.add(suiteResults[i])
                }
            })
        } else {
            test.runTests(result => {
                results.add(result)
            })
        }
        
        let fancy = new FancyOutputStream(headerPattern: "/\\")
        
        let unsuccessful = results.filter({ !_.success })
        
        if (unsuccessful.count > 0) {
            fancy.writeHeader("#unsuccessful.count Failure#{unsuccessful.count == 1 ? "" : "s"} out of #results.count test#{results.count == 1 ? "" : "s"}", pattern: ":'( ", symmetrical: true)
            
            unsuccessful.forEach({
                Console.log("#_")
            })
        } else {
            fancy.writeHeader("All #results.count Successful", pattern: ":) ", symmetrical: true)
        }
        
        timer.stop()

        Console.writeLine("Took " + timer.duration + "ms")

        if (unsuccessful.count > 0) {
            System.exit(1)
        }
    }
}